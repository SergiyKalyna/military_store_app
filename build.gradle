plugins {
    id 'java'
    id 'org.jetbrains.gradle.plugin.idea-ext'
    id 'org.owasp.dependencycheck'
    id 'checkstyle'
}

ext.libs = [
        unitTest: ["org.junit.jupiter:junit-jupiter-api",
                   "org.junit.jupiter:junit-jupiter-params",
                   "org.junit.jupiter:junit-jupiter-engine",
                   "org.mockito:mockito-core",
                   "org.mockito:mockito-junit-jupiter",
                   "org.assertj:assertj-core"],
        lombok  : 'org.projectlombok:lombok:1.18.30',
        slf4j  : 'org.slf4j:slf4j-api:2.0.12'
]

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'

    repositories {
        mavenCentral()
    }

    group = 'com.militarystore'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
            vendor = JvmVendorSpec.ADOPTIUM
        }
    }

    dependencies {
        implementation platform("org.springframework.boot:spring-boot-dependencies:${spring_boot_version}")
        implementation("org.springframework.boot:spring-boot-starter-security")
    }

    test {
        useJUnitPlatform()
    }

    checkstyle {
        toolVersion = '10.6.0'
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
        configProperties = ["suppressionFile": rootProject.file('config/checkstyle/suppressions.xml')]
        sourceSets = [project.sourceSets.main, project.sourceSets.test] as Collection<SourceSet>
    }
}

wrapper {
    gradleVersion = "8.6"
    distributionType = Wrapper.DistributionType.ALL
}

dependencyCheck {
    cveValidForHours = 24
    failOnError = false
    format = 'ALL'
}